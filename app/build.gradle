import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("../iPay_KeyStore/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    signingConfigs {
        iPaySigningConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    defaultConfig {
        applicationId "bd.com.ipay.android"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 146
        versionName "1.7.2"
        multiDexEnabled true
    }

    lintOptions {
        abortOnError false
    }

    // Server Type 1 -> dev server
    // Server Type 2 -> test server
    // Server Type 3 -> stage server
    // Server Type 4 -> live server
    // Server Type 5 -> local server
    def ServerType = java.lang.System.getProperty('ServerType') ?: "4"
    def AutoLogin = java.lang.System.getProperty('AutoLogin') ?: "false"
    def DebugOnOffStatus = java.lang.System.getProperty('DebugOnOffStatus') ?: "false"

    buildTypes {
        debug {
            buildConfigField "int", "ServerType", ServerType
            buildConfigField "boolean", "AutoLogin", AutoLogin
            buildConfigField "boolean", "DebugOnOffStatus", DebugOnOffStatus
        }
        release {
            signingConfig signingConfigs.iPaySigningConfig
            buildConfigField "int", "ServerType", ServerType
            buildConfigField "boolean", "AutoLogin", AutoLogin
            buildConfigField "boolean", "DebugOnOffStatus", DebugOnOffStatus
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    useLibrary 'org.apache.http.legacy'
    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/main/assets/fonts'] } }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.aspectj:aspectjtools:1.8.1'
    }
}

android.packagingOptions {
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/license.txt'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/notice.txt'
    exclude 'META-INF/ASL2.0'
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

configurations.all {
    resolutionStrategy {
        force 'com.android.support:design:25.3.1'
        force 'com.android.support:support-v4:25.3.1'
        force 'com.android.support:appcompat-v7:25.3.1'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':camera')
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.journeyapps:zxing-android-embedded:3.0.1@aar'
    compile 'com.google.zxing:core:3.2.0'
    compile 'com.airbnb.android:lottie:2.5.0'
    compile 'com.makeramen:roundedimageview:2.2.1'
    compile 'com.github.bumptech.glide:glide:3.8.0'
    compile 'de.hdodenhof:circleimageview:2.2.0'
    compile('com.github.afollestad.material-dialogs:core:0.8.5.3@aar') {
        transitive = true
    }
    compile('org.apache.httpcomponents:httpmime:4.3.6') {
        exclude module: 'httpclient'
    }
    compile 'org.apache.httpcomponents:httpclient-android:4.3.5'
    compile 'org.apache.commons:commons-lang3:3.0'

    // Bottom Sheet
    compile 'com.flipboard:bottomsheet-core:1.5.0'
    compile 'com.flipboard:bottomsheet-commons:1.5.0'

    compile files('libs/libphonenumber-7.2.8.jar')
    compile 'com.github.johnkil.android-progressfragment:progressfragment:1.4.0'
    compile 'com.google.firebase:firebase-messaging:11.2.0'
    compile 'com.google.android.gms:play-services-places:11.2.0'
    compile 'com.google.android.gms:play-services-vision:11.2.0'
    compile 'com.google.android.gms:play-services-analytics:11.2.0'
    compile 'com.google.android.gms:play-services:11.2.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.firebase:firebase-client-android:2.4.0'

    // Notification badge
    compile 'com.mikepenz:actionitembadge:3.2.6@aar'
    compile 'com.mikepenz:iconics-core:2.6.6@aar'

    // Aspect
    compile 'org.aspectj:aspectjrt:1.8.1'

    // Multiple image picker
    compile 'com.github.esafirm.android-image-picker:imagepicker:1.3.2@aar'

    // SSL pinning
    compile 'com.squareup.okhttp:okhttp:2.1.0'

    //Intercom
    compile 'io.intercom.android:intercom-sdk-base:3.+'
    compile 'io.intercom.android:intercom-sdk-fcm:3.+'

    // Singapore
    compile 'com.hbb20:ccp:1.7.8'

    testCompile 'junit:junit:4.12'
}

final def log = project.logger
final def variants = project.android.applicationVariants

variants.all { variant ->
    JavaCompile javaCompile = variant.javaCompile
    javaCompile.doLast {
        String[] args = ["-showWeaveInfo",
                         "-1.5",
                         "-inpath", javaCompile.destinationDir.toString(),
                         "-aspectpath", javaCompile.classpath.asPath,
                         "-d", javaCompile.destinationDir.toString(),
                         "-classpath", javaCompile.classpath.asPath,
                         "-bootclasspath", project.android.bootClasspath.join(File.pathSeparator)]
        log.debug "ajc args: " + Arrays.toString(args)

        MessageHandler handler = new MessageHandler(true);
        new Main().run(args, handler);
        for (IMessage message : handler.getMessages(null, true)) {
            switch (message.getKind()) {
                case IMessage.ABORT:
                case IMessage.ERROR:
                case IMessage.FAIL:
                    log.error message.message, message.thrown
                    break;
                case IMessage.WARNING:
                    log.warn message.message, message.thrown
                    break;
                case IMessage.INFO:
                    log.info message.message, message.thrown
                    break;
                case IMessage.DEBUG:
                    log.debug message.message, message.thrown
                    break;
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
